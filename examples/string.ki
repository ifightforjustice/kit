import bar;

extern malloc fn[u32, *void];
extern free fn[*void, void];

func[T] alloc_array(n u32) *T {
  var u union { vp *void; tp *T; };
  u.vp = malloc(n * @[u32]~sizeof@[T]);
  for var i u32 = 0u; i < n; i = i + 1u {
    init(&u.tp[i]);
  }
  return u.tp;
}

def[T] delete_array = func(n u32, p *T) void {
  for var i u32 = 0u; i < n; i = i + 1u {
    destroy(&p[i]);
  }
  var u union { vp *void; tp *T; };
  u.tp = p;
  free(u.vp);
};

def alloc_u8_array = func(n u32) *u8 {
  var u union { vp *void; u8p *u8; };
  u.vp = malloc(n);
  return u.u8p;
};

def free_u8_array = func(p *u8) void {
  var u union { vp *void; u8p *u8; };
  u.u8p = p;
  free(u.vp);
};

defclass move string struct {
  size u32;
  cap u32;
  buf *u8;
};

access string {
  def do_init = func(p *string) void {
    p->size = 0u;
    p->cap = 0u;
    var buf *u8;
    p->buf = buf;
  };

  def do_destroy = func(p *string) void {
    delete_array(p->cap, p->buf);
  };

  def do_copy = func(p *string, c *string) void {
    p->size = c->size;
    p->cap = c->cap;
    p->buf = alloc_array@[u8](p->size);
    for var i u32 = 0u; i < c->size; i = i + 1u {
      p->buf[i] = c->buf[i];
    }
  };

  def grow_cap = func(p *string, newcap u32) void {
    var buf = alloc_array@[u8](newcap);
    var sz u32 = p->size;
    for var i u32 = 0u; i < sz; i = i + 1u {
      buf[i] = p->buf[i];
    }

    delete_array(p->cap, p->buf);
    p->buf = buf;
    p->cap = newcap;
  };

  def append = func(p *string, c u8) void {
    if p->size == p->cap {
      var new_cap u32;
      if (p->cap == 0u) {
        new_cap = 8u;
      } else {
        new_cap = p->cap + p->cap;
      }
      grow_cap(p, new_cap);
    }

    p->buf[p->size] = c;
    p->size = p->size + 1u;
  };

  def get = func(p *string, i u32) u8 {
    return p->buf[i];
  };

  def count = func(p *string) u32 {
    return p->size;
  };

  func[T] lits(arr T) string {
    var ret string;
    var sz u32 = arr.length;
    for var i u32 = 0u; i < sz; i = i + 1u {
      append(&ret, @[u8] arr[i]);
    }
    return ret;
  }
}

def print = func(p *string) void {
  for var i u32 = 0u; i < count(p); i = i + 1u {
    var ch i32 = ~get(p, i);
    putchar(ch);
  }
};
