import state;

// This file's a grab-bag for lifetime type related stuff.  Probably most of it should be moved to other files and this should go away.

// I suspect instpacks might require lifetime type name mapping information, so this is a placeholder.
func canonicalized_pack(pack *opt[te_instpack]) opt[te_instpack] {
  return *pack;
}

func canonicalize_arraytype_count(a *opt[u32]) opt[u32] {
  return *a;
}

func make_canonicalized_lifetime_name(im *identmap, ix size) sym {
  b array[u8];
  push(&b, '$');
  build_u8str(void, &b, &ix);
  return intern(im, from_u8(&b));
}

func canonicalize_idy_lifetimes(im *identmap, lifetime_names *array[sym], t *te_typeexpr, out *te_typeexpr) void {
  switch t {
  case &TeName(a te_name):
    *out = *t;
  case &TeApp(a te_typeapp):
    newparams array[te_typeexpr];
    nparams size = count(&a.params);
    for i size = 0; i < nparams; i++ {
      tmp te_typeexpr;
      canonicalize_idy_lifetimes(im, lifetime_names, ref(&a.params, i), &tmp);
      push(&newparams, tmp);
    }
    *out = TeApp({a.typefunc, freeze(&newparams), canonicalized_pack(&a.pack)});
  case &TeStruct(a te_struct):
    *out = TeStruct({canonicalize_idy_vardecls(im, lifetime_names, &a.fields)});
  case &TeEnum(a te_enum):
    *out = TeEnum({canonicalize_idy_vardecls(im, lifetime_names, &a.constructors)});
  case &TeUnion(a te_union):
    // Honestly, a union having lifetimes would be pretty weird.
    *out = TeUnion({canonicalize_idy_vardecls(im, lifetime_names, &a.fields)});
  case &TeArraytype(a te_arraytype):
    count opt[u32] = canonicalize_arraytype_count(&a.count);
    param shp[te_typeexpr];
    canonicalize_idy_lifetimes(im, lifetime_names, oo(&a.param), oo(&param));
    *out = TeArraytype({count, param});
  case &TeLifetime(a te_lifetime):
    if case Has(ix size) = find(lifetime_names, a.name) {
      *out = TeLifetime({make_canonicalized_lifetime_name(im, ix)});
    } else {
      n size = count(lifetime_names);
      push(lifetime_names, a.name);
      *out = TeLifetime({make_canonicalized_lifetime_name(im, n)});
    }
  case &TeUnknown(v void):
    ice(_u8("canonicalize_idy_lifetimes sees TeUnknown."));
  }
}

func canonicalize_idy_vardecls(im *identmap, lifetime_names *array[sym], a *shray[te_vardecl]) shray[te_vardecl] {
  build array[te_vardecl];
  n size = count(a);
  for i size = 0; i < n; i++ {
    decl *te_vardecl = ref(a, i);
    tmp te_typeexpr;
    canonicalize_idy_lifetimes(im, lifetime_names, &decl->type, &tmp);
    push(&build, {decl->name, tmp});
  }
  return freeze(&build);
}

func is_parm_generic(im *identmap, num_lifetime_types_used size, name sym) bool {
  for i size = 0; i < num_lifetime_types_used; i++ {
    if make_canonicalized_lifetime_name(im, i) == name {
      return true;
    }
  }
  return false;
}
