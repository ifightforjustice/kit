import state;

func[H] build_u8str(help H, r *array[u8], x *lives) void {
  build_array_u8str(help, r, &x->all);
}

func dumbly_analyze_returning_expression(h *frame, gr *frame_graph, liveset *lives, gn gr_num) np {
  mbmut genexpr_result;
  if case Printed(pm) = dumbly_analyze_expression(h, gr, liveset, gn, &mbmut) {
    return Printed(pm);
  }
  switch &mbmut {
  case &Terminal:
    return NoFail;
  case &NonTerminal:
    return ERR(_u8("ICE: Expecting terminal expression, for gn "), gn.x, _u8(" in "), lookup(h->im, gr->informal_name));
  }
}

enum genexpr_result {
  Terminal void;
  NonTerminal void;
};


func dumbly_analyze_expression(h *frame, gr *frame_graph, liveset *lives, gn gr_num, out *genexpr_result) np {
  gnc *gn_annot = ref_annot(h, gn);
  if case &Computed(live lives_checksum) = &gnc->plainly_live {
    // TODO: We should also check that we're part of the same sequential _family_ of exprs.
    if !(liveset->xored == live.xored && liveset->summed == live.summed) {
      return ERR(_u8("ICE: Inconsistent liveness state when revisiting node, liveset="), *liveset);
    }
    *out = NonTerminal;
    return NoFail;
  }

  annotate(&gnc->plainly_live, {liveset->xored, liveset->summed});

  node *gr_node = ref_node(gr, gn);
  switch node {
  case &XOp(xn gr_xnode):
    nprecs size = count(&xn.precs);
    for i size = 0; i < nprecs; i = i + 1 {
      if case Printed(pm) = dumbly_analyze_returning_expression(h, gr, liveset, get(&xn.precs, i)) {
        return Printed(pm);
      }
    }
    switch &xn.op {
    case &GrApply(a gr_apply):
      // Retcell needs to be live _before_ the funcall happens.
      input(liveset, a.retcell);

      note_lower_paramlists(h, gn, liveset);
      annotate_ref(&gnc->app_time_live, liveset);

      deaden(liveset, a.funcell);
      nparams size = count(&a.params);
      for i size = 0; i < nparams; i = i + 1 {
        deaden(liveset, get(&a.params, i));
      }
      *out = Terminal;
      return NoFail;

    case &GrPrimApply(a gr_prim_apply):
      input(liveset, a.retcell);

      nparams size = count(&a.params);
      for i size = 0; i < nparams; i = i + 1 {
        deaden(liveset, get(&a.params, i));
      }
      *out = Terminal;
      return NoFail;

    case &GrMemCopy(a gr_memcopy):
      input(liveset, a.dest);
      input(liveset, a.src);
      *out = Terminal;
      return NoFail;

    case &GrWriteConst(a gr_writeconst):
      input(liveset, a.dest);
      *out = Terminal;
      return NoFail;

    case &GrAddressof(a gr_addressof):
      input(liveset, a.addressee);
      output(h, liveset, a.dest);
      *out = Terminal;
      return NoFail;

    case &GrDeref(a gr_deref):
      input(liveset, a.pointer);
      check(!isLocationStatic(ref_cell(gr, a.name)->location));
      create(h, liveset, a.name);
      *out = Terminal;
      return NoFail;

    case &GrSubcell(a gr_subcell):
      input(liveset, a.partof);
      check(!isLocationStatic(ref_cell(gr, a.name)->location));
      create(h, liveset, a.name);
      *out = Terminal;
      return NoFail;

    case &GrLive(a gr_live):
      create(h, liveset, a.cell);
      *out = Terminal;
      return NoFail;

    case &GrAssertLive(a gr_assert_live):
      input(liveset, a.cell);
      *out = Terminal;
      return NoFail;

    case &GrDead(a gr_dead):
      deaden(liveset, a.cell);
      *out = Terminal;
      return NoFail;

    case &GrVirtualDead(a gr_virtual_dead):
      deaden(liveset, a.cell);
      *out = Terminal;
      return NoFail;

    case &GrXNop:
      *out = Terminal;
      return NoFail;

    case &GrPrint(a gr_print):
      *out = Terminal;
      return NoFail;
    }

  case &QOp(qn gr_qnode):
    switch &qn.op {
    case &GrBranch(a gr_branch):
      input(liveset, a.src);

      end_lives lives;
      combined_mut genexpr_result = NonTerminal;
      ncases size = count(&a.cases);
      for i size = 0; i < ncases; i = i + 1 {
        cas *tup[gr_const, sq_num] = ref(&a.cases, i);
        new_live lives = *liveset;
        casmut genexpr_result;
        if case Printed(pm) = dumbly_analyze_expression(h, gr, &new_live, cas->cdr.x, &casmut) {
          return Printed(pm);
        }
        combine_branch_mut(&new_live, &combined_mut, casmut, &end_lives);
      }

      if case Has(def_gn sq_num) = a.default_case {
        new_live lives = *liveset;
        defcasmut genexpr_result;
        if case Printed(pm) = dumbly_analyze_expression(h, gr, &new_live, def_gn.x, &defcasmut) {
          return Printed(pm);
        }
        combine_branch_mut(&new_live, &combined_mut, defcasmut, &end_lives);
      }

      // Yes this is empty of it's NonTerminal -- it doesn't matter.
      *liveset = end_lives;
      *out = combined_mut;
      return NoFail;

    case &GrSequence(a gr_sequence):
      if case Printed(pm) = dumbly_analyze_returning_expression(h, gr, liveset, a.first) {
        return Printed(pm);
      }
      return dumbly_analyze_expression(h, gr, liveset, a.second.x, out);

    case &GrJmp(a gr_jmp):
      return dumbly_analyze_expression(h, gr, liveset, a.next.x, out);

    case &GrQNop:
      *out = Terminal;
      return NoFail;
    }
  }
}

func note_lower_paramlists(h *frame, our_gn gr_num, live_at_app_time *lives) void {
  paramlists array[gr_num];
  nall size = count(&live_at_app_time->all);
  for i size = 0; i < nall; i = i + 1 {
    c cell_num = get(&live_at_app_time->all, i);
    if case Has(gn gr_num) = ref(&h->bas->celldisp, c.x)->paramlist {
      if gn != our_gn {
        push(&paramlists, gn);
        discard bool = try_insert(&ref_annot(h, gn)->higher_paramlists, &our_gn, void);
      }
    }
  }

  sort(&paramlists);
  dedup_sorted(&paramlists);
  annotate_move(&ref_annot(h, our_gn)->lower_paramlists, &paramlists);
}

func combine_branch_mut(new_live *lives, onto *genexpr_result, case_mut genexpr_result, end_lives *lives) void {
  switch case_mut {
  case Terminal:
    switch *onto {
    case Terminal:
      if new_live->xored != end_lives->xored || new_live->summed != end_lives->summed {
        ice(_u8("terminal/terminal new_live not equal, end_lives="), *end_lives, _u8(", new_live="), *new_live);
      }
    case NonTerminal:
      *onto = Terminal;
      *end_lives = *new_live;
    }
  case NonTerminal: { }
  }
}

// For deaden and other functions, prelive shows what was alive _before_ prec_muts modified it.
func deaden(liveset *lives, c cell_num) void {
  if !try_remove(&liveset->all, c) {
    ice(_u8("deaden on unlive cell "), c);
  }
  liveset->xored = liveset->xored ^ ~c.x;
  liveset->summed = liveset->summed - ~c.x;
}

// input requires that the cell already be live.
func input(liveset *lives, c cell_num) void {
  if case None = lookup(&liveset->all, c) {
    ice(_u8("input on unlive cell "), c);
  }
}

// output either makes the cell live, if it wasn't already, or it means we use the cell.
// (TODO(): We're adding GrLive, so this should become obsolete.)
func output(h *frame, liveset *lives, c cell_num) void {
  if try_insert(&liveset->all, c) {
    liveset->xored = liveset->xored ^ ~c.x;
    liveset->summed = liveset->summed + ~c.x;
    note_conflicting(h, &liveset->all, c);
  }
}

func create(h *frame, liveset *lives, c cell_num) void {
  // Both these places define the virtual cell.  So it should not be live.
  if !try_insert(&liveset->all, c) {
    ice(_u8("create on live cell "), c);
  }
  liveset->xored = liveset->xored ^ ~c.x;
  liveset->summed = liveset->summed + ~c.x;
  note_conflicting(h, &liveset->all, c);
}

func note_conflicting(h *frame, all *array[cell_num], c cell_num) void {
  // This also means conflicting is set to non-empty for any cell that's made live.
  c_disp *cell_disp = ref(&h->bas->celldisp, c.x);
  nall size = count(all);
  for i size = 0; i < nall; i = i + 1 {
    d cell_num = get(all, i);
    d_disp *cell_disp = ref(&h->bas->celldisp, d.x);
    push(&c_disp->conflicting, d);
    push(&d_disp->conflicting, c);
  }
}

func ref_annot(h *frame, gn gr_num) *gn_annot {
  return ref(&h->by_gn, gn.x);
}
