import array;
import graph;
import io;
import parse;
import stdlib;
import string;
import test_check;

// TODO: We probably don't use win_objfile, linux_objfile, or objfile here -- these were included at one point to make sure they get compiled.
import build;
import linux_objfile;
import win_objfile;
import objfile;

func run_tests() bool {
  test_parse_passed bool = test_parse();
  if test_parse_passed {
    print(Stdout(), _u8("parse_test passes\n"));
  } else {
    print(Stdout(), _u8("parse_test FAIL\n"));
  }
  fflush(Stdout());
  test_check_res tup[bool, u32] = test_check();
  if test_check_res.car {
    print(Stdout(), _u8("check_test passes\n"));
  } else {
    print(Stdout(), _u8("check_test FAIL ("), to_u8str(test_check_res.cdr), _u8(" failures)\n"));
  }
  fflush(Stdout());
  test_bigint_passed bool = test_bigint();
  if test_bigint_passed {
    print(Stdout(), _u8("check_bigint passes\n"));
  } else {
    print(Stdout(), _u8("check_bigint FAIL\n"));
  }
  fflush(Stdout());
  all_success bool = test_parse_passed && test_check_res.car && test_bigint_passed;
  if all_success {
    print(Stdout(), _u8("All tests PASS!\n"));
  } else {
    print(Stdout(), _u8("FAIL FAIL FAIL\n"));
  }
  fflush(Stdout());
  return all_success;
}

func file_loader(cs *checkstate, name sym) opt[array[u8]] {
  ns array[u8] = to_u8str(lookup(cs->im, name));
  return read_file(concat(ns, _u8(".ki")));
}

func run_on_file(modulename array[u8]) bool {
  im identmap = make_im();
  cs checkstate = make_checkstate(&im, x86_platform(), null, file_loader);
  add_primitives(&cs);
  if !check_module(&cs, intern(&im, from_u8(modulename))) {
    print(Stdout(), _u8("Failed.\n"));
    fflush(Stdout());
    return false;
  } else {
    print(Stdout(), _u8("Success.\n"));
    fflush(Stdout());
    return true;
  }
}

// TODO: c_int
export func main(argc i32, argv **u8) i32 {
  if argc == 1 {
    if !run_tests() {
      return 1;
    }
  } else if argc == 2 {
    if !run_on_file(lpz_as_u8str(argv[1])) {
      return 1;
    }
  } else {
    print(Stderr(), _u8("Usage: "), lpz_as_u8str(argv[0]), _u8(" [<filename>]\n"));
    return 1;
  }

  return 0;
}
