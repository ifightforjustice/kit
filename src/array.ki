import stdlib;

defclass[T] move array struct {
  count size;
  cap size;
  ptr *T;
};

access array[_] {
  func[T] do_init(r *array[T]) void {
    r->count = 0u;
    r->cap = 0u;
    var null *T;
    r->ptr = null;
  }

  func[T] do_copy(dest *array[T], src *array[T]) void {
    dest->count = src->count;
    dest->cap = src->cap;
    dest->ptr = @[*T] cast(malloc(src->count * @[size]~sizeof@[T]));
    var e size = src->count;
    for var i size = ~0u; i < e; i = i + @[size]~1u {
      dest->ptr[i] = src->ptr[i];
    }
  }

  func[T] do_destroy(r *array[T]) void {
    for var i size = r->count; i.~ > 0u; {
      i.~ = i.~ - 1u;
      destroy(&r->ptr[i]);
    }
    free(@[*void] cast(r->ptr));
  }

  func[T] set(r *array[T], i size, val T) void {
    check(i < r->count);
    (*r)[i] = val;
  }

  func[T] get(r *array[T], i size) T {
    check(i < r->count);
    return (*r)[i];
  }

  func[T] count(r *array[T]) size {
    return r->size;
  }

  func[T] change_cap_(r *array[T], newcap size) void {
    check(newcap >= r->count);
    var ptr *T = @[*T] cast(malloc(newcap * @[size]~sizeof@[T]));
    var e size = r->count;
    for var i size = ~0u; i < e; i = i + @[size]~1u {
      move(&ptr[i], &r->ptr[i]);
    }
    free(@[*void] cast(r->ptr));
    r->ptr = ptr;
    r->cap = newcap;
  }

  func[T] append(r *array[T], value T) void {
    if (r->count == r->cap) {
      change_cap_(r, max(@[size]~4u, r->cap * @[size]~2u));
    }
    // The reason we use copy, not move, here, is because value's
    // destructor will get called when we exit this function.
    copy(&r->ptr[r->count], &value);
    r->count = r->count + 1;
  }
}

func foo() void {
  var v array[array[i32]];
  var u = v;
}

