import stdlib;

// Shared pointers.  Right now many users of these just don't want to
// copy, the way box does, because non-copying movable types are not
// possible yet.

deftype[T] shp_pointee struct {
  refcount size;
  value T;
};

defclass[T] move shp *shp_pointee[T];

access shp[_] {
  func[T] do_init(u *shp[T]) void {
    u->~ = @[*shp_pointee[T]]cast(checkmalloc(sizeof@[shp_pointee[T]]));
    init(&u->~ ->value);
    u->~ ->refcount = 1;
  }

  func[T] do_copy(dest *shp[T], src *shp[T]) void {
    src->~ ->refcount = src->~ ->refcount + 1;
    dest->~ = src->~;
  }

  func[T] do_destroy(u *shp[T]) void {
    rc size = u->~ ->refcount - 1;
    u->~ ->refcount = rc;
    if rc == 0 {
      destroy(u->~);
      free(@[*void]cast(u->~));
    }
  }

  func[T] oo(x *shp[T]) *T {
    return &x->~ ->value;
  }

  func[T] oo(x shp[T]) *T {
    return &x.~ ->value;
  }
}

func[T] emshp(val T) shp[T] {
  ret shp[T];
  *oo(&ret) = val;
  return ret;
}
