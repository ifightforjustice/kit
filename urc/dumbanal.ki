import state;

func[H] build_u8str(help H, r *array[u8], x *cellset) void {
  build_array_u8str(help, r, &x->all);
}

func dumbly_analyze_returning_expression(h *frame, gr *frame_graph, liveset *cellset, gn gr_num) np {
  mbmut genexpr_result;
  if case Printed(pm) = dumbly_analyze_expression(h, gr, liveset, gn, &mbmut) {
    return Printed(pm);
  }
  switch &mbmut {
  case &Terminal:
    return NoFail;
  case &NonTerminal:
    return ERR(_u8("ICE: Expecting terminal expression, for gn "), gn.x, _u8(" in "), lookup(h->im, gr->informal_name));
  }
}

enum genexpr_result {
  Terminal void;
  NonTerminal void;
}


func dumbly_analyze_expression(h *frame, gr *frame_graph, liveset *cellset, gn gr_num, out *genexpr_result) np {
  gnc *gn_annot = ref_annot(h, gn);
  if case &Computed(live cellset_checksum) = &gnc->plainly_live {
    // TODO: We should also check that we're part of the same sequential _family_ of exprs.
    if !matches_checksum(liveset, &live) {
      return ERR(_u8("ICE: Inconsistent liveness state when revisiting node, liveset="), *liveset);
    }
    *out = NonTerminal;
    return NoFail;
  }

  annotate(&gnc->plainly_live, {liveset->xored, liveset->summed});

  node *gr_node = ref_node(gr, gn);
  switch node {
  case &XOp(xn gr_xnode):
    switch &xn.op {
    case &GrApply(a gr_apply):
      // Retcell needs to be live _before_ the funcall happens.
      expect_live(liveset, a.retcell);

      note_lower_paramlists(h, gn, liveset);
      annotate_ref(&gnc->app_time_live, liveset);

      deaden(liveset, a.funcell);
      nparams size = count(&a.params);
      for i size = 0; i < nparams; i = i + 1 {
        deaden(liveset, get(&a.params, i));
      }
      *out = Terminal;
      return NoFail;

    case &GrPrimApply(a gr_prim_apply):
      expect_live(liveset, a.retcell);

      nparams size = count(&a.params);
      for i size = 0; i < nparams; i = i + 1 {
        deaden(liveset, get(&a.params, i));
      }
      *out = Terminal;
      return NoFail;

    case &GrMemCopy(a gr_memcopy):
      expect_live(liveset, a.dest);
      expect_live(liveset, a.src);
      *out = Terminal;
      return NoFail;

    case &GrWriteConst(a gr_writeconst):
      expect_live(liveset, a.dest);
      *out = Terminal;
      return NoFail;

    case &GrAddressof(a gr_addressof):
      expect_live(liveset, a.addressee);
      expect_live(liveset, a.dest);
      *out = Terminal;
      return NoFail;

    case &GrDeref(a gr_deref):
      expect_live(liveset, a.pointer);
      check(!isLocationStatic(ref_cell(gr, a.name)->location));
      create(h, liveset, a.name);
      *out = Terminal;
      return NoFail;

    case &GrSubcell(a gr_subcell):
      expect_live(liveset, a.partof);
      check(!isLocationStatic(ref_cell(gr, a.name)->location));
      create(h, liveset, a.name);
      *out = Terminal;
      return NoFail;

    case &GrLive(a gr_live):
      create(h, liveset, a.cell);
      *out = Terminal;
      return NoFail;

    case &GrAssertLive(a gr_assert_live):
      expect_live(liveset, a.cell);
      *out = Terminal;
      return NoFail;

    case &GrDead(a gr_dead):
      deaden(liveset, a.cell);
      *out = Terminal;
      return NoFail;

    case &GrVirtualDead(a gr_virtual_dead):
      deaden(liveset, a.cell);
      *out = Terminal;
      return NoFail;
    }

  case &QOp(qn gr_qnode):
    switch &qn.op {
    case &GrBranch(a gr_branch):
      expect_live(liveset, a.src);

      end_liveset cellset;
      combined_mut genexpr_result = NonTerminal;
      ncases size = count(&a.cases);
      for i size = 0; i < ncases; i = i + 1 {
        cas *tup[gr_const, sq_num] = ref(&a.cases, i);
        new_live cellset = *liveset;
        casmut genexpr_result;
        if case Printed(pm) = dumbly_analyze_expression(h, gr, &new_live, cas->cdr.x, &casmut) {
          return Printed(pm);
        }
        combine_branch_mut(&new_live, &combined_mut, casmut, &end_liveset);
      }

      if case Has(def_gn sq_num) = a.default_case {
        new_live cellset = *liveset;
        defcasmut genexpr_result;
        if case Printed(pm) = dumbly_analyze_expression(h, gr, &new_live, def_gn.x, &defcasmut) {
          return Printed(pm);
        }
        combine_branch_mut(&new_live, &combined_mut, defcasmut, &end_liveset);
      }

      // Yes this is empty of it's NonTerminal -- it doesn't matter.
      *liveset = end_liveset;
      *out = combined_mut;
      return NoFail;

    case &GrSequence(a gr_sequence):
      if case Printed(pm) = dumbly_analyze_returning_expression(h, gr, liveset, a.first) {
        return Printed(pm);
      }
      return dumbly_analyze_expression(h, gr, liveset, a.second.x, out);

    case &GrJmp(a gr_jmp):
      return dumbly_analyze_expression(h, gr, liveset, a.next.x, out);

    case &GrQNop:
      *out = Terminal;
      return NoFail;
    }
  }
}

func note_lower_paramlists(h *frame, our_gn gr_num, live_at_app_time *cellset) void {
  paramlists array[gr_num];
  nall size = count(&live_at_app_time->all);
  for i size = 0; i < nall; i = i + 1 {
    c cell_num = get(&live_at_app_time->all, i);
    if case Has(gn gr_num) = ref(&h->bas->celldisp, c.x)->paramlist {
      if gn != our_gn {
        push(&paramlists, gn);
        discard bool = try_insert(&ref_annot(h, gn)->higher_paramlists, &our_gn, void);
      }
    }
  }

  sort(&paramlists);
  dedup_sorted(&paramlists);
  annotate_move(&ref_annot(h, our_gn)->lower_paramlists, &paramlists);
}

func combine_branch_mut(new_live *cellset, onto *genexpr_result, case_mut genexpr_result, end_liveset *cellset) void {
  switch case_mut {
  case Terminal:
    switch *onto {
    case Terminal:
      if !matches_checksum(new_live, end_liveset) {
        ice(_u8("terminal/terminal new_live not equal, end_liveset="), *end_liveset, _u8(", new_live="), *new_live);
      }
    case NonTerminal:
      *onto = Terminal;
      *end_liveset = *new_live;
    }
  case NonTerminal: { }
  }
}

// For deaden and other functions, prelive shows what was alive _before_ prec_muts modified it.
func deaden(liveset *cellset, c cell_num) void {
  if !try_remove(&liveset->all, c) {
    ice(_u8("deaden on unlive cell "), c);
  }
  liveset->xored = liveset->xored ^ ~c.x;
  liveset->summed = liveset->summed - ~c.x;
}

func expect_live(liveset *cellset, c cell_num) void {
  if case None = lookup(&liveset->all, c) {
    ice(_u8("expect_live on unlive cell "), c);
  }
}

func create(h *frame, liveset *cellset, c cell_num) void {
  gr_create(liveset, c);
  note_conflicting(h, &liveset->all, c);
}

// Used in graph code because we don't note conflicting and don't have a frame (yet).
func gr_create(liveset *cellset, c cell_num) void {
  if !try_insert(&liveset->all, c) {
    ice(_u8("create on live cell "), c);
  }
  liveset->xored = liveset->xored ^ ~c.x;
  liveset->summed = liveset->summed + ~c.x;
}

func note_conflicting(h *frame, all *array[cell_num], c cell_num) void {
  // This also means conflicting is set to non-empty for any cell that's made live.
  c_disp *cell_disp = ref(&h->bas->celldisp, c.x);
  nall size = count(all);
  for i size = 0; i < nall; i = i + 1 {
    d cell_num = get(all, i);
    d_disp *cell_disp = ref(&h->bas->celldisp, d.x);
    push(&c_disp->conflicting, d);
    push(&d_disp->conflicting, c);
  }
}

func ref_annot(h *frame, gn gr_num) *gn_annot {
  return ref(&h->by_gn, gn.x);
}
